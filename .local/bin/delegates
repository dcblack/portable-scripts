#!/usr/bin/env /usr/bin/perl

require v5.6;
use strict;
use English;
use JSON;
use FindBin qw($RealBin $RealScript);
use FileHandle;
use Cwd;
STDOUT->autoflush(1);
STDERR->autoflush(1);

if( $ARGV[0] =~ m/^-{1,2}h(elp)?$/ ) {
  my $documentation = <<"EOT";
Name
----

$RealScript - extract delegate information from CoCalc courses

Synopsis
--------

  $RealScript --help|-h
  $RealScript [-doall|-ssh|-who=_PATT_] _COURSEFILE(S)_...
  $RealScript _EMAIL_

Description
-----------

Extracts hostname and email information from CoCalc course files (JSON format).
If a debrief*.md file exists, the names will also be extracted if contains lines
of the format:

FIRST LAST <EMAIL\@COMPANY.DOMAIN>;

Options
-------

OPTION      | DESCRIPTION
------      | -----------
-doall      | generates a bash script to visit all delegate machines and perform an action
--help|-h   | display this help text
-ssh        | generates \$HOME/.ssh/config entries instead of a simple list
-ssh/config | greps your ~/.ssh/config file for delegates
-who        | allows filtering by delegate e-mail address.

--------------------------------------------------------------------------------
EOT
  print $documentation;
  exit 1;
}

# Parse command-line
my %OPT;
while( $ARGV[0] =~ m/^-/ ) {
  my $opt = shift @ARGV;
  $opt =~ s{^--+}{-};
  if( $opt =~ m/=/ ) {
    $OPT{$`} = $';
  } else {
    $OPT{$opt}=$;;
  }#endif
}
# Shortcuts
$OPT{-verbose} = $OPT{-v} if exists $OPT{-v};
$OPT{-list}    = $OPT{-l} if exists $OPT{-l};

my $sshconfig = "$ENV{HOME}/.ssh/config";
$sshconfig = $OPT{-ssh} if exists $OPT{-ssh} and $OPT{-ssh} ne $;;
$sshconfig = $OPT{'-ssh/config'} if exists $OPT{'-ssh/config'} and $OPT{'-ssh/config'} ne $;;

# Open debrief markdown file and search for lines matching: FirstName LastName <MailName@Domain>
# Use this information to match e-mail address in Cocalc course to their real name.
our %name;
for my $debrief (glob("debrief[-_]*.md")) {
  printf "Parsing %s\n", $debrief if exists $OPT{-verbose};
  open DEBRIEF,'<',$debrief;
  while(not eof DEBRIEF) {
    my $line = <DEBRIEF>;
    # Remove leading/trailing whitespace
    chomp $line;
    chomp $line;
    $line =~ s/^\s+//;
    $line =~ s/\s+$//;
    next unless $line =~ m{^(\w[^<\@\|]+\w) +<([^>\@ ]+[\@][^ \@>]+)>};
    my ($name,$email) = ($1,lc $2);
    $name=~ s/\s+/ /g;
    $email=~ s/\s+/ /g;
    $name{$email} = $name;
  }
  close DEBRIEF;
  printf "Found %d names\n", scalar keys %name if exists $OPT{-verbose};
}
if( exists $OPT{-names} ) {
  my @found;
  for my $key (keys %name) {
    push @found,"$name{$key} <$key>;";
  }
  printf "Names:\n  %s\n",join("\n  ",sort @found);
  exit 0;
}

if( scalar @ARGV == 0 and exists $OPT{'-ssh/config'}) {
  printf "From ~/.ssh/config:\n";
  system("/usr/bin/egrep '^Host delegate[0-9]' $sshconfig");
  exit 0;
}

if( scalar @ARGV == 0 ) {
  for my $course (sort glob("*.course")) {
    push @ARGV,$course;
  }
}


if( $ARGV[0] =~ m/[@]/ ) {
  open PIPE,qq{/usr/bin/egrep '^Host delegate[0-9].*$ARGV[0]' "$sshconfig"|};
  while(<PIPE>) {
    my @F = split;
    printf "ssh %s\n", $F[1];
  }
  close PIPE;
  exit 0;
}

my $date = `date`;
if( exists $OPT{-ssh} ) {
  $sshconfig = "config" if $sshconfig eq "$ENV{HOME}/.ssh/config";
  open SSHCONFIG,'>',$sshconfig;
  print SSHCONFIG << "EOT";
# ssh config file (place under $ENV{HOME}/.ssh/ as 'config')
# created on $date

Host localhost
    HostKeyAlias myself

EOT
}
my $doall = 'doall.sh';
if( exists $OPT{-doall} ) {
  open DOALL,'>',$doall;
  printf DOALL <<'EOT' if exists $OPT{-doall};
#!/usr/bin/env bash

source "${HOME}/.local/scripts/Essential-IO"
function Report_info() {
  local ESC
  ESC=$(perl -le "print chr(27)")
  echo "${ESC}[95,01m$*${ESC}[00m";
}
EOT
}

for my $course_file ( @ARGV ) {
  if( $course_file !~ m/[.]course$/ ) {
    printf STDERR "Error: file $course_file does not end with .course -- ignored\n";
    next;
  }
  printf "Parsing %s\n", $course_file if exists $OPT{-verbose};
  printf SSHCONFIG "#{ Delegates from %s\n\n", $course_file if exists $OPT{-ssh};
  unless ( exists $OPT{-ssh} or exists $OPT{-who} or exists $OPT{-list} ) {
    printf " N  | Name | Email | Cocalc User\n";
    printf "--- | ---- | ----- | -----------\n";
  }
  open COURSE,'<',$course_file;
  my $idx = 0;
  while(<COURSE>) {
    next unless m/"account_id"/;
    my $course_json = decode_json($_);
    next unless defined $course_json->{"email_address"};
    ++$idx;
    my $email = lc $course_json->{"email_address"};
    my $user  = $course_json->{"project_id"};
    $user =~ s/-//g;
    my $name = $name{$email};
    if( exists $OPT{-ssh} ) {
      printf SSHCONFIG "Host delegate%d # %s <%s>;\n", $idx, $name, $email;
      printf SSHCONFIG "    User   %s\n", $user;
      printf SSHCONFIG "    Hostname ssh.cocalc.com\n";
      printf SSHCONFIG "\n";
    } elsif( exists $OPT{-doall} ) {
      my $who = $OPT{-who};
      next unless $who ne $; and not ($email =~ m/$who/i or $name{$email} =~ m/$who/i);
      my $info=sprintf( "%d %s <%s>", $idx, $name, $email );
      printf DOALL "Report_info '%s' ssh %s\@ssh.cocalc.com ;\n", $info, $user;
    } elsif( exists $OPT{-who} ) {
      my $who = $OPT{-who};
      next unless $email =~ m/$who/i or $name{$email} =~ m/$who/i;
      printf "ssh %s\@ssh.cocalc.com ;# %d %s <%s>\n", $user, $idx, $name, $email;
    } elsif( exists $OPT{-list} ) {
      printf "%s\@ssh.cocalc.com\n", $user;
    } else {
      printf "%d | %s | %s | %s\@ssh.cocalc.com\n", $idx, $name, $email, $user;
    }
  }#endwhile
  printf "Found %d students in course file\n", $idx if exists $OPT{-verbose};
  printf( SSHCONFIG "#} Delegates above here from  %s\n", $course_file ) if exists $OPT{-ssh};
  close COURSE;

}#endfor

if( exists $OPT{-ssh} ) {
  print SSHCONFIG << "EOT";

# Site-wide defaults for various options
Host *
    PubkeyAcceptedKeyTypes=+ssh-dss
    Ciphers aes128-ctr,aes192-ctr,aes256-ctr
    ForwardX11 no
    ForwardX11Trusted yes
    ForwardAgent yes
    ServerAliveCountMax 10
    ServerAliveInterval 45
    SendEnv SSHUSER_*

# vim:nospell:syntax=sshconfig
EOT
  close SSHCONFIG;
  printf "Created %s\n", $sshconfig;
}

if( exists $OPT{-doall} ) {
  printf( DOALL "# created on %s", $date );
  close DOALL;
  printf "Created %s\n", $doall;
}

exit 0;

# vim:syntax=perl
