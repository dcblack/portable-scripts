#!/usr/bin/env perl
#
# $Info: markdown-vim Adds yaml front-matter and updates timestamps in markdown. $
#
# Uses environment variables to determine TITLE, SUBJECT, AUTHOR, and EMAIL
# Date must be formatted as a triplet
# Time must be formatted [0-9]{1,2}:[0-9][0-9] with optional AM/PM

require 5.4.0;
use strict;
use warnings;
use English;

# Get the current date components
my ($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime;
$year += 1900;  # Convert from years since 1900 to full year
my $mm = sprintf("%02d",$mon+1);
my $dd = sprintf("%02d",$mday);
my @days = qw(Sunday Monday Tuesday Wednesday Thursday Friday Saturday);
my @months = qw(January February March April May June July August September October November December);
my $ampm = ( $hour >= 12 ) ? "PM" : "AM";
my $hr12 = ($hour==0)?12:($hour>12)?$hour-12:$hour;
my $time = sprintf("%02d:%02d %s",$hr12,$min,$ampm);

my $date = "$year-".$months[$mon]."-$mday";
my $title   = '{:TITLE:}';
$title = $ENV{TITLE} if exists $ENV{TITLE};
my $subject = '{:SUBJECT:}';
$subject = $ENV{SUBJECT} if exists $ENV{SUBJECT};
my $author = '';
$author = $ENV{AUTHOR_NAME} if $author eq '' and exists $ENV{AUTHOR_NAME};
$author = $ENV{AUTHOR}      if $author eq '' and exists $ENV{AUTHOR};
$author = $ENV{NAME}        if $author eq '' and exists $ENV{NAME};
$author = $ENV{USER}        if $author eq '' and exists $ENV{USER};
$author = '{:AUTHOR:}' if $author eq '';
my $email = '';
$email = $ENV{HOME_EMAIL} if $email eq '' and exists $ENV{HOME_EMAIL};
$email = $ENV{WORK_EMAIL} if $email eq '' and exists $ENV{WORK_EMAIL};
$email = $ENV{EMAIL}      if $email eq '' and exists $ENV{EMAIL};
$email   = '{:EMAIL:}'    if $email eq '';
my $yaml =
"---
title: ${title}
subject: ${subject}
author: ${author}
contacts: ${email}
---
[Updated ${date} ${time}]
[Version 0.0]
";

my $inp_name = "@ARGV";
my $new_name = $inp_name;
$new_name =~ s{([.][[:word:]]+)$}{-new$1};
my $old_name = $inp_name;
$old_name =~ s{([.][[:word:]]+)$}{-old$1};
open my $inp_file, '<', $inp_name or die "Unable to open $inp_name";
open my $new_file, '>', $new_name or die "Unable to open $new_name";
while(<$inp_file>) {
  # next if $INPUT_LINE_NUMBER > 50;
  $_ = $yaml.$_ if $INPUT_LINE_NUMBER < 3 and s{^\[(frontmatter|yaml|yml)\]}{};
  s{\[(Timestamp|Edited|Revised|Modified|Updated) \S+[-/:]\S+[-/:][0-9A-Za-z]+}{[$1 ${date}}i;
  s{\[((Timestamp|Edited|Revised|Modified|Updated) \S+[-/:]\S+[-/:]\S+) \d{1,2}:\d\d( ?(AM|PM))?}{[$1 ${time}}i;
  if(m{\[(Version |v)(\d+)[.](\d+)}i) {
    my ($tag,$maj,$min) = ($1,$2,$3);
    ++$min;
    s{\[(Version |v)\d+[.]\d+}{[${tag}${maj}.${min}}i;
  }
} continue {
  print $new_file $_;
}
rename $inp_name, $old_name;
rename $new_name, $inp_name;
unlink $old_name;
